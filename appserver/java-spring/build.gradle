/*
 * Samplestack gradle build file
 */
import groovy.json.JsonOutput

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

/* spring boot build dependency */
buildscript {
	ext {
		springBootVersion = '1.2.1.RELEASE'
	}
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

/* plugins */
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

group = samplestackGroup
version = samplestackVersion
sourceCompatibility = samplestackSourceCompatibility

/*
 * Create the database-properties.json file from mldb prefixed entries in gradle.properties
 */
task dbConfigFile {
    def keys = project.properties.keySet()
    def forestSize = 3
    def dbname = project.property("marklogic.rest.name")
    def map = [:]
    for (key in keys) {
        if (key.size() > 4 && "mldb." == key[0..4]) {
            def dbprop = key[5..(key.size()-1)]
            def lastdot = dbprop.lastIndexOf('.') 
            if (dbprop == "database-name") {
                dbname = project.property(key)
            } else if (dbprop == "forest-size") {
                forestSize = project.property(key).toLong()
            } else {
                def value = project.property(key)
                if (value == 'false' || value == 'true') {
                    value = value.toBoolean()
                } else if (value.isDouble()) {
                    value = value.toDouble()
                } else if (value.isLong()) {
                    value = value.toLong()
                }
                map.put(dbprop, value)
            }
        }
    }
    map.put("database-name", dbname)
    def forest = []
    for (i in (1..forestSize)) {
        forest.add(dbname + "-" + String.valueOf(i).padLeft(3, "0"))
    }
    map.put("forest", forest)
    map.put("forests-per-host", forestSize)

    def file = new File("../../database/database-properties.json")
    file.write(JsonOutput.prettyPrint(JsonOutput.toJson(map)))
}

/*
 * INIT
 * Run this task to initialize a fresh MarkLogic server
 * and install security objects.
 */
task dbInit(type: MarkLogicInitTask)
dbInit.roles = file("../../database/security/roles")
dbInit.users = file("../../database/security/users")

/*
 * TEARDOWN
 * This task removes all trace of samplestack
 */
task dbTeardown(type: MarkLogicTeardownTask)

/*
 * CONFIGURE
 *
 * This task configures the database and REST API instance
 * based on files in the database directory
 */
task dbConfigure(type: MarkLogicConfigureTask)
dbConfigFile.dependsOn dbConfigFile

task dbConfigureClean << {
    delete("${buildDir}/database")
}


dbConfigure.inputDir = file('../../database')
dbConfigure.outputDir = file("${buildDir}/database")
dbConfigure.inputProperty = project.properties['taskInputProperty'] ?: "original"
dbConfigure.shouldRunAfter dbInit
dbConfigure.shouldRunAfter dbConfigureClean
dbInit.dependsOn(dbConfigureClean)

task dbConfigureAll
dbConfigureAll.dependsOn dbConfigureClean
dbConfigureAll.dependsOn dbConfigure

/**
 * ASSEMBLE
 */
assemble.dependsOn(dbInit, dbConfigure, test)

/*
 * CLEAR
 */
task dbClear(type: MarkLogicClearTask)


/* use same properties for gradle and Java runtime */
task props(type: Copy) {
    from('.') {
        include 'gradle.properties'
    }
    into 'src/main/resources'
}

/*
 * appserver
 * assembles the project, loads sample data and starts the appserver on port 8090
 */
task appserver <<
{
   println "Bootstrapping, seeding and starting Samplestack appserver"
}
appserver.dependsOn(clean, assemble, bootRun)
bootRun.shouldRunAfter(assemble)

/* The code repositories to consult for dependencies */
repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.3.7"
    compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.security:spring-security-ldap:4.0.0.RC1")
    compile("org.springframework.security:spring-security-web:4.0.0.RC1")
    compile("org.springframework.security:spring-security-test:4.0.0.RC1")
    compile("org.apache.directory.server:apacheds-server-jndi:1.5.5")
    compile("org.apache.directory.server:apacheds-bootstrap-partition:1.5.5")
    compile("org.apache.directory.server:apacheds-bootstrap-extract:1.5.5")
    compile("joda-time:joda-time:2.4")
    compile('com.marklogic:java-client-api:3.0.1') {
        exclude(group: 'org.slf4j')
        exclude(group: 'ch.qos.logback')
    }
    testCompile('com.jayway.jsonpath:json-path:1.1.0') {
        exclude(group: 'org.slf4j')
    }
    testCompile('org.skyscreamer:jsonassert:1.2.3')
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

test.dependsOn(props, dbConfigure)

/* task to generate the gradle wrapper script */
task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

/* set springboot related properties */
tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
    def keys = project.properties.keySet()
    for (key in keys) {
        if (key.size() > 7 && "spring." == key[0..6]) {
            systemProperty(key[7..(key.size()-1)], project.property(key))
        }
    }
}
